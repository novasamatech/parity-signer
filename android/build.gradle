plugins {
	id 'com.android.application'
	id 'kotlin-android'
	id 'org.mozilla.rust-android-gradle.rust-android'
	id 'idea'
	// Add the Google services Gradle plugin
  id 'com.google.gms.google-services'
}

apply plugin: 'kotlin-android'
apply plugin: 'com.google.gms.google-services'

android {
	defaultConfig {
		applicationId "io.parity.signer"
		minSdk 23
		targetSdk 34
		versionCode 70200
		versionName "7.2.0"
		ndk {
			abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
		}

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
		vectorDrawables {
			useSupportLibrary true
		}
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}
	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_11
		targetCompatibility JavaVersion.VERSION_11
	}
	kotlinOptions {
		jvmTarget = '11'
	}
	buildFeatures {
		compose true
	}
	composeOptions {
		kotlinCompilerExtensionVersion compose_compiler_version
	}
	packagingOptions {
		resources {
			excludes += '/META-INF/{AL2.0,LGPL2.1}'
		}
		jniLibs {
			pickFirsts += ['lib/armeabi-v7a/libc++_shared.so', 'lib/arm64-v8a/libc++_shared.so', 'lib/x86/libc++_shared.so', 'lib/x86_64/libc++_shared.so']
		}
	}
	compileSdk 34
	ndkVersion '24.0.8215888'
    namespace 'io.parity.signer'
}

android.applicationVariants.all { variant ->
	def uniffiPath = "${buildDir}/generated/source/uniffi/${variant.name}/java"
	def t = tasks.register("generate${variant.name.capitalize()}UniFFIBindings", Exec) {
		workingDir "${project.projectDir}"
		// Runs the bindings generation, note that you must have uniffi-bindgen installed and in your PATH environment variable
		commandLine 'uniffi-bindgen', 'generate', "${workingDir}/../rust/signer/src/signer.udl", '--language', 'kotlin', '--out-dir', uniffiPath
	}
	variant.javaCompileProvider.get().dependsOn(t)
	def sourceSet = variant.sourceSets.find { it.name == variant.name }
	sourceSet.java.srcDir new File(uniffiPath)
	// XXX: I've been trying to make this work but I can't, so the compiled bindings will show as "regular sources" in Android Studio.
	idea.module.generatedSourceDirs += file("${uniffiPath}/uniffi")
}
apply plugin: 'org.mozilla.rust-android-gradle.rust-android'

cargo {
	module = "../rust/signer"
	targetDirectory = "../rust/target"
	libname = "signer"
	targets = ["arm", "arm64", "x86_64"]
	apiLevel = 24 //NDK API level, not SDK
	profile = 'release'
	extraCargoBuildArguments = ["--locked"]
}

tasks.matching { it.name.matches(/merge.*JniLibFolders/) }.configureEach {
	it.inputs.dir(new File(buildDir, "rustJniLibs/android"))
	it.dependsOn("cargoBuild")
}

task buildDB (type: Exec) {
	commandLine './generate_database.sh'
}

dependencies {
	implementation "androidx.core:core-ktx:1.12.0"
	implementation 'androidx.appcompat:appcompat:1.6.1'
	implementation 'com.google.android.material:material:1.9.0'
	implementation 'androidx.activity:activity-compose:1.8.2'
	implementation 'androidx.biometric:biometric:1.1.0'
	implementation "androidx.compose.ui:ui:$compose_libs_version"
	implementation "androidx.compose.material:material:$compose_libs_version"
	implementation "androidx.compose.material:material-icons-extended:$compose_libs_version"
	implementation "androidx.compose.ui:ui-tooling-preview:$compose_libs_version"
	implementation "com.google.accompanist:accompanist-permissions:$accompanist_version"
	implementation "androidx.datastore:datastore-preferences:1.0.0"
	implementation "androidx.navigation:navigation-compose:2.7.7"
	implementation "androidx.lifecycle:lifecycle-runtime-compose:2.7.0"
	implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.7.0'
	implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.7.0'
	implementation 'androidx.security:security-crypto:1.1.0-alpha06'
	implementation 'androidx.security:security-crypto-ktx:1.1.0-alpha06'
	implementation "androidx.camera:camera-core:$androidx_camera_version"
	implementation "androidx.camera:camera-lifecycle:$androidx_camera_version"
	implementation "androidx.camera:camera-view:$androidx_camera_version"
	implementation "androidx.camera:camera-camera2:$androidx_camera_version"
	implementation "com.google.mlkit:barcode-scanning:17.2.0"
	implementation "com.halilibo.compose-richtext:richtext-commonmark-android:0.20.0"
	implementation "com.halilibo.compose-richtext:richtext-ui-material-android:0.20.0"
	implementation "org.jetbrains.kotlinx:kotlinx-collections-immutable:0.3.7"
	implementation "net.java.dev.jna:jna:5.13.0@aar"
	implementation "com.appmattus.crypto:cryptohash:0.10.1" //black2b hash for dot icon
	implementation "io.coil-kt:coil-compose:$coilVersion"
	implementation "io.coil-kt:coil-svg:$coilVersion"
	implementation 'com.jakewharton.timber:timber:5.0.1'
	testImplementation 'junit:junit:4.13.2'
	testImplementation "org.mockito:mockito-core:5.10.0"
	testImplementation "androidx.test:core:1.5.0"
	testImplementation "androidx.test.ext:junit:1.1.5"
	testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.8.0'
	androidTestImplementation 'junit:junit:4.13.2'
	androidTestImplementation 'androidx.test.ext:junit:1.1.5'
	androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
	androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_libs_version"
	debugImplementation "androidx.compose.ui:ui-tooling:$compose_libs_version"
	// Import the Firebase BoM
  implementation platform('com.google.firebase:firebase-bom:33.12.0')
}

preBuild.dependsOn "cargoBuild","buildDB"
